plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'idea'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

version = project.mod_version
group = project.maven_group
base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }

    maven {
        name 'terraformersmc Maven'
        url "https://maven.terraformersmc.com"
    }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testmodImplementation sourceSets.main.output
}

loom {
    accessWidenerPath = file("src/main/resources/biome_modifier.accessWidener")

    runs {
        server {
            runDir "run/server"
        }
        client {
            runDir "run/client"
        }
        testmodServer {
            runDir "run/testServer"
            server()
            ideConfigGenerated project.rootProject == project
            source sourceSets.testmod
        }
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
